How do I keep track of groupings certain elements are part of?
<p>I apologize for the shoddy title; I have no idea how to properly describe the issue I am having.</p> <p>I have multiple tab-delimited files in the following format:</p> <pre><code>groupA donuts apples groupB car dog ball meter groupC apples donuts car groupD ball shirt pencil paper donuts </code></pre> <p>with differing number of rows.</p> <p>For each line, the first word is the group name while the rest of line are names of objects. What I want to do is keep track of what groups each object is part of. So in this example, I would find that <code>ball</code> is part of <code>groupD</code> and <code>groupB</code> while <code>car</code> is only part of <code>groupC</code>. <code>apples</code> is part of <code>groupA</code> and <code>groupC</code> while <code>pencil</code> is only part of <code>groupD</code>. </p> <p>Since each file I'm reading in has differing number of rows/groups, what is the best way to go about accomplishing this?</p> <pre><code>#!/usr/bin/perl use strict; use warnings; my $path = "../GENELIST.symbols.csv"; open(PATH, $path) || die "cannot open csv\n"; my @groups = (); while(my $line = &lt;PATH&gt;){ if($line =~ /^(\w+)\t/){ push(@groups, $1); } } close(PATH); #at this point I have the name of all the groups in the particular file (`groupA`, `groupB`, `groupC`, `groupD`). </code></pre>
<p>Just use a hash of arrays.</p> <p>To get more familiar with such structures, check out: <a href="http://perldoc.perl.org/perldsc.html"><code>Perl Data Structures Cookbook</code></a></p> <pre><code>use strict; use warnings; my %groups; while (&lt;DATA&gt;) { my ($group, @cols) = split; push @{$groups{$_}}, $group for @cols; } use Data::Dump; dd \%groups; __DATA__ groupA donuts apples groupB car dog ball meter groupC apples donuts car groupD ball shirt pencil paper donuts </code></pre> <p>Outputs:</p> <pre><code>{ apples =&gt; ["groupA", "groupC"], ball =&gt; ["groupB", "groupD"], car =&gt; ["groupB", "groupC"], dog =&gt; ["groupB"], donuts =&gt; ["groupA", "groupC", "groupD"], meter =&gt; ["groupB"], paper =&gt; ["groupD"], pencil =&gt; ["groupD"], shirt =&gt; ["groupD"], } </code></pre>