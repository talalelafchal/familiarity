Which format is this config file in?
<p>I have to parse config files which look like this:</p> <pre><code>begin key1 "value1" key2 "value2" begin key3 "value3" key4 "value4" end end </code></pre> <p>What is this format and is there a ready-made parser I can use (preferably in Perl/Python)?</p>
<p>I don't know the name of the format, but <a href="https://metacpan.org/pod/Parser%3a%3aMGC" rel="nofollow">Parser::MGC</a> can make light work of this one.</p> <p>By defining a simple self-recursive <code>parse</code> method that recognises the <code>begin</code> blocks as inner scopes, we can build a recursive tree of HASH references from the input.</p> <pre><code>package MyParser; use base 'Parser::MGC'; use strict; use warnings; # Need to exclude linefeeds from the whitespace pattern sub pattern_ws { qr/[\t ]+/ } sub parse { my $self = shift; my %items; $self-&gt;sequence_of( sub { $self-&gt;any_of( sub { # begin ... end block $self-&gt;expect( 'begin' ); $self-&gt;commit; $self-&gt;expect( qr/\n/ ); $self-&gt;scope_of( undef, sub { push @{$items{begin}}, $self-&gt;parse; }, 'end' ); $self-&gt;expect( qr/\n/ ); }, sub { # key "value" my $key = $self-&gt;token_ident; $self-&gt;commit; $items{$key} = $self-&gt;token_string; $self-&gt;expect( qr/\n/ ); }, ) }); return \%items; } </code></pre> <p>This can be printed at the end, perhaps using <code>Data::Dump</code>:</p> <pre><code>use Data::Dump 'pp'; print STDERR pp(MyParser-&gt;new-&gt;from_file(\*STDIN)); </code></pre> <p>This gives</p> <pre><code>{ begin =&gt; [ { begin =&gt; [{ key3 =&gt; "value3", key4 =&gt; "value4" }], key1 =&gt; "value1", key2 =&gt; "value2", }, ], } </code></pre>