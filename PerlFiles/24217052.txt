Delete Perl Hash Values if duplication found
<p>I need a quick help , I'm greping some commands in a Unix Server and creating a hash out of those commands. </p> <p>Issue is most of the time , there are duplicate Values which are coming and I want to remove any duplicate value of out these Hash. </p> <p>Here is an example : </p> <pre><code>[randy@server04 ~/scripts]$ perl snmpperl.pl $VAR1 = { '1b' =&gt; [ 'abc_pl', 'abc_pl', 'abc_pl', 'xyz_pl', 'xyz_pl', ], '1a' =&gt; [ 'abc_pl', 'abc_pl', 'abc_pl', 'abc_pl', 'xyz_pl', 'xyz_pl', ] </code></pre> <p>I need the Hash to be :- </p> <pre><code>$VAR1 = { '1b' =&gt; [ 'abc_pl', 'xyz_pl', ], '1a' =&gt; [ 'abc_pl', 'xyz_pl', ] </code></pre>
<p>This is a relatively common Perl idiom, and is actually addressed in the FAQ, which you could locate by typing <code>perldoc -q duplicate</code> on any system with Perl installed.</p> <p>Here is an adaptation on the ideas expressed in the FAQ:</p> <pre><code>use strict; use warnings; use Data::Dumper; my %hash = ( '1b' =&gt; [ 'abc_pl', 'abc_pl', 'abc_pl', 'xyz_pl', 'xyz_pl', ], '1a' =&gt; [ 'abc_pl', 'abc_pl', 'abc_pl', 'abc_pl', 'xyz_pl', 'xyz_pl', ], ); foreach my $v ( values %hash ) { my %seen; @$v = grep { !$seen{$_}++ } @$v; } print Dumper \%hash; </code></pre> <p>This works by keeping track of whether any given element in the sub-array for a given hash key has been seen before. If not, pass it through the grep filter. Otherwise, don't send it through. In the end, all that gets built into the new structure are single instances of the array elements.</p> <p>One nuance worth mentioning; The "it" variable in a <code>foreach</code> loop becomes an alias to the element it is representing. So in this case, for each iteration of the loop, <code>$v</code> aliases a hash element who's value contains an anonymous array reference. We simply replace contents of the anonymous array ref with the de-duped elements.</p>
<pre><code>use List::MoreUtils 'uniq'; @$_ = uniq @$_ for values %hash; </code></pre> <p>Replacement for <code>uniq</code> from <a href="https://metacpan.org/pod/List%3a%3aMoreUtils" rel="nofollow"><code>List::MoreUtils</code></a></p> <pre><code>sub uniq (@) { my %seen; grep !$seen{$_}++, @_; } </code></pre>