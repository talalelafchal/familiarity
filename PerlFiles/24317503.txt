modify lines between two tags in perl
<p>I need some help with replaceing lines between two tages in perl. I have a file in which I want to modify lines between two tags:</p> <pre><code>some lines some lines tag1 ABC somelines NOP NOP ABC somelines NOP NOP ABC somelines tag2 </code></pre> <p>As you can see, I have two tags, tag1 and tag2 and basically, I want to replace all instances of ABC with NOP between tag1 and tag2. Here is the relevant portion of code but it doesn't replace. Can anyone please help..?</p> <pre><code> my $fh; my $cur_file = "file_name"; my @lines = (); open($fh, '&lt;', "$cur_file") or die "Can't open the file for reading $!"; print "Before while\n"; while(&lt;$fh&gt;) { print "inside while\n"; my $line = $_; if($line =~ /^tag1/) { print "inside range check\n"; $line = s/ABC/NOP/; push(@lines, $line); } else { push(@lines, $line); } } close($fh); open ($fh, '&gt;', "$cur_file") or die "Can't open file for wrinting\n"; print $fh @lines; close($fh); </code></pre>
<p>Consider a one-liner using the <a href="http://www.serverwatch.com/tutorials/article.php/3853051/Perl-and-the-FlipFlop-Operator.htm" rel="nofollow">Flip-Flop</a> operator.</p> <pre><code>perl -i -pe 's/ABC/NOP/ if /^tag1/ .. /^tag2/' file </code></pre>
<p>Your line which says <code>$line = s/ABC/NOP/;</code> is incorrect, you need <code>=~</code> there.</p> <pre><code>#!/usr/bin/perl use strict; use warnings; my $tag1 = 0; my $tag2 = 0; while(my $line = &lt;DATA&gt;){ if ($line =~ /^tag1/){ $tag1 = 1; #Set the flag for tag1 } if ($line =~ /^tag2/){ $tag2 = 1; #Set the flag for tag2 } if($tag1 == 1 &amp;&amp; $tag2 == 0){ $line =~ s/ABC/NOP/; } print $line; } </code></pre> <p><a href="http://ideone.com/DGF2M2" rel="nofollow"><strong>Demo</strong></a></p>
<p>Use <a href="http://perldoc.perl.org/perlvar.html" rel="nofollow"><code>$INPLACE_EDIT</code></a> in conjunction with the range operator <a href="http://perldoc.perl.org/perlop.html#Range-Operators" rel="nofollow"><code>..</code></a></p> <pre><code>use strict; use warnings; local $^I = '.bak'; local @ARGV = $cur_file; while (&lt;&gt;) { if (/^tag1/ .. /^tag2/) { s/ABC/NOP/; } print; } unlink "$cur_file$^I"; #delete backup; </code></pre> <p>For alternative ways to edit a file, check out: <a href="http://perldoc.perl.org/perlfaq5.html#How-do-I-change%2c-delete%2c-or-insert-a-line-in-a-file%2c-or-append-to-the-beginning-of-a-file%3f" rel="nofollow"><code>perlfaq5</code></a></p>