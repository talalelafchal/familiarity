How to subtract the values within array in perl?
<p>How to subtract the succeeding value by the preceding value?</p> <pre><code>use warnings; use strict; my @hus = qw(7 9 12 16 20 21); my ($i,$j); for($i = 0; $i &lt;= $#hus; $i++){ for($j = 1; $j &lt;= $#hus; $j++){ my $m = $hus[$j]-$hus[$i]; print "$m\n"; } } </code></pre> <p>In this program gives the output. But i expect particular output such as 2 3 4 4 1. This output is comes from 9-7=2, 12-9=3, 16-12=4, 20-16=4, 21-20=1. How to print the particular outputs?</p>
<p>You only need to loop through the array once.</p> <pre><code>use warnings; use strict; my @hus = qw( 7 9 12 16 20 21 ); for (my $i = 0; $i &lt; $#hus; ++$i) { my $m = $hus[$i+1] - $hus[$i]; print "$m\n"; } </code></pre> <p>Output:</p> <pre><code>2 3 4 4 1 </code></pre>
<p>You need 5 iterations for 6 elements, or more generically, you need to loop one fewer times than the number of elements in <code>@hus</code>. This whole nested loop approach is wrong because <code>$i</code> should always be one less than <code>$j</code>. Fixed:</p> <pre><code>my ($i,$j); for ($j = 1; $j &lt;= $#hus; $j++) { my $i = $j-1; my $m = $hus[$j]-$hus[$i]; print "$m\n"; } </code></pre> <p>But let's clean that up.</p> <pre><code>for my $i (1..$#hus) { print $hus[$i]-$hus[$i-1], "\n"; } </code></pre> <p>You could even reduce it to the following:</p> <pre><code>say $hus[$_]-$hus[$_-1] for 1..$#hus; </code></pre>
<p>You can do this quite compactly:</p> <pre><code>use strict; use warnings; my @hus = qw(7 9 12 16 20 21); print $hus[$_] - $hus[$_-1], "\n" for 1 .. $#hus; </code></pre> <p>The expression <code>1 .. $#hus</code> generates a list of numbers from 1 to the index of the last element of the array <code>@hus</code>. The <code>for</code> loop goes through this list, assigning the current value to the special variable <code>$_</code>. This means that the two elements you're interested in are at position <code>$_</code> and <code>$_-1</code> of <code>@hus</code>.</p> <p>Output:</p> <pre><code>2 3 4 4 1 </code></pre>
<p>You are calculating <em>forward differences</em>. It can be done very simply like this</p> <pre><code>use strict; use warnings; my @hus = qw( 7 9 12 16 20 21 ); my @diff = map { $hus[$_] - $hus[$_-1] } 1 .. $#hus; print "@diff\n"; </code></pre> <p><strong>output</strong></p> <pre><code>2 3 4 4 1 </code></pre> <hr> <p><strong>Update</strong></p> <p>Perhaps you want the output that you show in your question? You can do that like this</p> <pre><code>use strict; use warnings; my @hus = qw(7 9 12 16 20 21); print join ', ', map { my ($i, $j) = @hus[$_-1, $_]; sprintf('%d-%d=%d', $j, $i, $j-$i); } 1 .. $#hus; print "\n"; </code></pre> <p><strong>output</strong></p> <pre><code>9-7=2, 12-9=3, 16-12=4, 20-16=4, 21-20=1 </code></pre>