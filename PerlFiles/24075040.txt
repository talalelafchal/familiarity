Mojo::DOM shortcut to get absolute url for a resource?
<p>When parsing a webpage with <a href="https://metacpan.org/pod/Mojo::DOM" rel="nofollow"><code>Mojo::DOM</code></a> (or any other framework), it's fairly common to be pulling a resource address that could be either relative or absolute. Is there a shortcut method to translate such a resource address to an absolute URL?</p> <p>The following <a href="https://metacpan.org/pod/distribution/Mojolicious/script/mojo" rel="nofollow"><code>mojo</code></a> command pulls all the stylesheets on mojolicio.us:</p> <pre><code>$ mojo get http://mojolicio.us "link[rel=stylesheet]" attr href /mojo/prettify/prettify-mojo-light.css /css/index.css </code></pre> <p>And the following script does the same, but also uses <a href="https://metacpan.org/pod/URI" rel="nofollow"><code>URI</code></a> to translate the resource into an absolute URL.</p> <pre><code>use strict; use warnings; use Mojo::UserAgent; use URI; my $url = 'http://mojolicio.us'; my $ua = Mojo::UserAgent-&gt;new; my $dom = $ua-&gt;get($url)-&gt;res-&gt;dom; for my $csshref ($dom-&gt;find('link[rel=stylesheet]')-&gt;attr('href')-&gt;each) { my $cssurl = URI-&gt;new($csshref)-&gt;abs($url); print "$cssurl\n"; } </code></pre> <p>Outputs:</p> <pre><code>http://mojolicio.us/mojo/prettify/prettify-mojo-light.css http://mojolicio.us/css/index.css </code></pre> <p>Obviously, a relative URL in this context should be made absolute using the URL that loaded DOM. However, I don't know of a way to get a resource absolute URL except for coding it myself.</p> <p>There is <a href="https://metacpan.org/pod/Mojo::URL#to_abs" rel="nofollow"><code>Mojo::URL #to_abs</code></a> in <code>Mojolicious</code>. However, I don't know if that would integrate in some way with <code>Mojo::DOM</code>, and by itself would take more code than <code>URI</code>.</p> <p>My ideal solution would be if something like the following were possible from both a script and command line, but looking for any related insights into using Mojo for parsing:</p> <pre><code>mojo get http://mojolicio.us "link[rel=stylesheet]" attr href to_abs </code></pre>
<p>I'm not sure why you think it would take more code to use <code>Mojo::URL</code>? In the following example I get the actual request URL from the transaction (there might have been redirects, which I've allowed) which I have called <code>$base</code>.</p> <p>Then since <code>$base</code> is an instance of <code>Mojo::URL</code> I can create a new instance with <code>$base-&gt;new</code>. Of course if that seems to magical, you can replace it with <code>Mojo::URL-&gt;new</code>.</p> <pre><code>use Mojo::Base -strict; use Mojo::UserAgent; my $url = 'http://mojolicio.us'; my $ua = Mojo::UserAgent-&gt;new-&gt;max_redirects(10); my $tx = $ua-&gt;get($url); my $base = $tx-&gt;req-&gt;url; $tx-&gt;res -&gt;dom -&gt;find('link[rel=stylesheet]') -&gt;map(sub{$base-&gt;new($_-&gt;{href})-&gt;to_abs($base)}) -&gt;each(sub{say}); </code></pre>